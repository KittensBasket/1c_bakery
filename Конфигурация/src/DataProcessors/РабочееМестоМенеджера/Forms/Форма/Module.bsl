&НаКлиенте
Процедура ЗаполнитьТочки(Команда)
    // Получаем данные текущей выделенной строки в таблице маршрутов
    ТекущиеДанныеМаршрута = Элементы.СписокМаршрутов.ТекущиеДанные;
    
    Если ТекущиеДанныеМаршрута <> Неопределено Тогда
        ЗаполнитьКлиентовПоМаршрутамНаСервере(ТекущиеДанныеМаршрута.Маршрут);
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКакВчера(Команда)
	ТекущиеДанные = Элементы.СписокКлиентов.ТекущиеДанные;
	ПеременнаяТекущийКлиент = ТекущиеДанные.Клиент; 
	ЗаполнитьСоставЗаказаКакВчераНаСервере(ПеременнаяТекущийКлиент);
КонецПроцедуры

&НаКлиенте
Процедура СписокКлиентовПриАктивизацииСтроки(Элемент)
    
    // Получаем ссылку на клиента из активизированной строки
    ТекущиеДанные = Элементы.СписокКлиентов.ТекущиеДанные;
    
    Если ТекущиеДанные <> Неопределено Тогда
        
        ПеременнаяТекущийКлиент = ТекущиеДанные.Клиент; 
        
        ЗаполнитьСоставЗаказаПоШаблонуНаСервере(ПеременнаяТекущийКлиент); 
    Иначе
        ПеременнаяТекущийКлиент = Неопределено;
        СоставЗаказа.Очистить();
    КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьКлиентовПоМаршрутамНаСервере(ВыбранныйМаршрут)
        
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    | МаршрутыСписокТочек.Клиент КАК Клиент,
    | ЛОЖЬ КАК Заказать
    |ИЗ
    | Справочник.Маршруты.СписокКлиентов КАК МаршрутыСписокТочек
    |ГДЕ
    | МаршрутыСписокТочек.Ссылка = &ВыбранныйМаршрут
    |УПОРЯДОЧИТЬ ПО
    | МаршрутыСписокТочек.НомерСтроки";
    
    Запрос.УстановитьПараметр("ВыбранныйМаршрут", ВыбранныйМаршрут);
    
    СписокКлиентов.Очистить();
    СписокКлиентов.Загрузить(Запрос.Выполнить().Выгрузить());
    
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоставЗаказаПоШаблонуНаСервере(КлиентСсылка)
    
    СоставЗаказа.Очистить();
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |    ШаблоныЗаказов.Продукция,
    |    ШаблоныЗаказов.Количество
    |ИЗ
    |    РегистрСведений.ШаблонЗаказов КАК ШаблоныЗаказов
    |ГДЕ
    |    ШаблоныЗаказов.Клиент = &Клиент";
    
    Запрос.УстановитьПараметр("Клиент", КлиентСсылка);
    
    ВыборкаШаблона = Запрос.Выполнить().Выбрать();
    
    Пока ВыборкаШаблона.Следующий() Цикл
        
        // Получаем текущую цену из регистра "ЦеныПродукции"
        Отбор = Новый Структура;
        Отбор.Вставить("Клиент", КлиентСсылка);
        Отбор.Вставить("Продукция", ВыборкаШаблона.Продукция);
        
        // Получаем цену на текущую дату
        ЗначенияРесурсов = РегистрыСведений.ЦеныПродукции.ПолучитьПоследнее(ТекущаяДата(), Отбор);
        
        ЦенаПродукции = 0;
        Если ЗначенияРесурсов.Свойство("Цена") Тогда
            ЦенаПродукции = ЗначенияРесурсов.Цена;
        КонецЕсли;
        
        Количество = ВыборкаШаблона.Количество;
        Сумма = Количество * ЦенаПродукции;
        
        НоваяСтрока = СоставЗаказа.Добавить();
        
        НоваяСтрока.Номенклатура = ВыборкаШаблона.Продукция; 
        НоваяСтрока.Количество = Количество;
        НоваяСтрока.Цена = ЦенаПродукции;
        НоваяСтрока.Сумма = Сумма;
        
    КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СписокКлиентовПриИзменении(Элемент)
    
    ТекущиеДанные = Элементы.СписокКлиентов.ТекущиеДанные;
    
    Если ТекущиеДанные <> Неопределено Тогда
        
        Если ТекущиеДанные.Заказать Тогда
            // Флажок установлен - вызываем создание заказа
            СоздатьЗаказКлиентаНаСервере(ТекущиеДанные.Клиент);

        КонецЕсли;
        
    КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоздатьЗаказКлиентаНаСервере(КлиентСсылка)
    
    Если СоставЗаказа.Количество() = 0 Тогда
        Сообщить("Нет шаблонов заказа. Заказ не создан");
        Возврат;
    КонецЕсли;
    
    НовыйЗаказ = Документы.Заказ.СоздатьДокумент();
    
    НовыйЗаказ.Клиент = КлиентСсылка;
    НовыйЗаказ.Дата = ТекущаяДата();
    // Дата отгрузки по умолчанию - завтрашняя дата 
    НовыйЗаказ.ДатаОтгрузки = НачалоДня(ТекущаяДата()) + 60*60*24; 
    
    Для Каждого СтрокаСостава Из СоставЗаказа Цикл
        
        НоваяСтрокаТЧ = НовыйЗаказ.Продукция.Добавить();
        НоваяСтрокаТЧ.Продукция = СтрокаСостава.Номенклатура;
        НоваяСтрокаТЧ.Количество = СтрокаСостава.Количество;
        НоваяСтрокаТЧ.Цена = СтрокаСостава.Цена;
        НоваяСтрокаТЧ.Сумма = СтрокаСостава.Сумма; 
        
    КонецЦикла;
    
    НовыйЗаказ.Записать(РежимЗаписиДокумента.Проведение);
    Сообщить("Для клиента " + КлиентСсылка + " создан Заказ клиента №" + НовыйЗаказ.Номер + " от " + НовыйЗаказ.Дата);
    
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоставЗаказаКакВчераНаСервере(КлиентСсылка)
    
    СоставЗаказа.Очистить();
    
    ДатаОтгрузкиВчера = НачалоДня(ТекущаяДата()) - 60*60*24;
    
    ЗапросНайтиЗаказ = Новый Запрос;
    ЗапросНайтиЗаказ.Текст =
    "ВЫБРАТЬ ПЕРВЫЕ 1
    | ЗаказКлиента.Ссылка
    |ИЗ
    | Документ.Заказ КАК ЗаказКлиента
    |ГДЕ
    | ЗаказКлиента.Клиент = &Клиент
    | И ЗаказКлиента.ДатаОтгрузки = &ДатаОтгрузкиВчера
    |УПОРЯДОЧИТЬ ПО
    | ЗаказКлиента.Дата УБЫВ";
    
    ЗапросНайтиЗаказ.УстановитьПараметр("Клиент", КлиентСсылка);
    ЗапросНайтиЗаказ.УстановитьПараметр("ДатаОтгрузкиВчера", ДатаОтгрузкиВчера);
    
    РезультатНайтиЗаказ = ЗапросНайтиЗаказ.Выполнить();
    ВыборкаНайтиЗаказ = РезультатНайтиЗаказ.Выбрать();
    
    Если Не ВыборкаНайтиЗаказ.Следующий() Тогда
        Сообщить("Заказ, исполненный вчера (" + Формат(ДатаОтгрузкиВчера, "ДФ=dd.MM.yyyy") + "), для клиента " + КлиентСсылка + " не найден.");
        Возврат;
    КонецЕсли;
    
    СсылкаНаЗаказВчера = ВыборкаНайтиЗаказ.Ссылка;
    
    ЗапросСостава = Новый Запрос;
    ЗапросСостава.Текст =
    "ВЫБРАТЬ
    | ЗаказКлиентаПродукция.Продукция КАК Продукция,
    | ЗаказКлиентаПродукция.Количество КАК Количество
    |ИЗ
    | Документ.Заказ.Продукция КАК ЗаказКлиентаПродукция
    |ГДЕ
    | ЗаказКлиентаПродукция.Ссылка = &СсылкаНаЗаказВчера";
    
    ЗапросСостава.УстановитьПараметр("СсылкаНаЗаказВчера", СсылкаНаЗаказВчера);
    
    ВыборкаПродукции = ЗапросСостава.Выполнить().Выбрать();
    
    ДатаОтгрузкиЗавтра = НачалоДня(ТекущаяДата()) + 60*60*24;
    
    Пока ВыборкаПродукции.Следующий() Цикл
        
        Отбор = Новый Структура;
        Отбор.Вставить("Клиент", КлиентСсылка);
        Отбор.Вставить("Продукция", ВыборкаПродукции.Продукция);
        
        ЗначенияРесурсов = РегистрыСведений.ЦеныПродукции.ПолучитьПоследнее(ДатаОтгрузкиЗавтра, Отбор);
        
        ЦенаПродукции = 0;
        Если ЗначенияРесурсов.Свойство("Цена") Тогда
            ЦенаПродукции = ЗначенияРесурсов.Цена;
        КонецЕсли;
        
        Количество = ВыборкаПродукции.Количество;
        Сумма = Количество * ЦенаПродукции;
        
        НоваяСтрока = СоставЗаказа.Добавить();
        
        НоваяСтрока.Номенклатура = ВыборкаПродукции.Продукция; 
        НоваяСтрока.Количество = Количество;
        НоваяСтрока.Цена = ЦенаПродукции;
        НоваяСтрока.Сумма = Сумма;
        
    КонецЦикла;

КонецПроцедуры