// При выборе машины подставляем её вместимость
&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		Объект.Вместимость = ПолучитьВместимостьНаСервере(Объект.ТранспортноеСредство);
	Иначе
		Объект.Вместимость = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВместимостьНаСервере(ТсСсылка)
	Возврат ТсСсылка.Вместимость; 
КонецФункции

&НаКлиенте
Процедура ЗаполнитьОтгрузки(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		Сообщить("Выберите транспортное средство!");
		Возврат;
	КонецЕсли;
	
	Если Объект.СписокМаршрутов.Количество() = 0 Тогда
		Сообщить("Добавьте хотя бы один маршрут в список.");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТаблицуОтгрузокНаСервере();
	
	// Проверка на перегруз после заполнения
	ТекущийВес = Объект.ОтгрузкиКлиентам.Итог("Вес");
	Если ТекущийВес > Объект.Вместимость Тогда
		Разница = ТекущийВес - Объект.Вместимость;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Удалите некоторые отгрузки! Вес продукции (" + ТекущийВес + " кг) превышает вместимость (" + Объект.Вместимость + " кг) на " + Разница + " кг!";
		Сообщение.Сообщить();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуОтгрузокНаСервере()
	
	Объект.ОтгрузкиКлиентам.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегМаршрутаМаршруты.Маршрут КАК Маршрут
		|ПОМЕСТИТЬ ВТ_ВыбранныеМаршруты
		|ИЗ
		|	&СписокМаршрутов КАК РегМаршрутаМаршруты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// 1. Собираем всех клиентов, включенных в выбранные маршруты
		|ВЫБРАТЬ
		|	МаршрутыКлиенты.Клиент КАК Клиент
		|ПОМЕСТИТЬ ВТ_КлиентыМаршрутов
		|ИЗ
		|	Справочник.Маршруты.СписокКлиентов КАК МаршрутыКлиенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВыбранныеМаршруты КАК ВТ_Маршруты
		|		ПО МаршрутыКлиенты.Ссылка = ВТ_Маршруты.Маршрут
		|
		|СГРУППИРОВАТЬ ПО
		|	МаршрутыКлиенты.Клиент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// 2. Выбираем все отгрузки, которые уже заняты в ДРУГИХ проведенных регистрациях
		|ВЫБРАТЬ
		|	РегМаршрутаОтгрузки.ОтгрузкаКлиенту КАК Отгрузка
		|ПОМЕСТИТЬ ВТ_ЗанятыеОтгрузки
		|ИЗ
		|	Документ.РегистрацияМаршрута.ОтгрузкиКлиентам КАК РегМаршрутаОтгрузки
		|ГДЕ
		|	РегМаршрутаОтгрузки.Ссылка.Проведен = ИСТИНА
		|	И РегМаршрутаОтгрузки.Ссылка <> &ТекущийДокумент
		|
		|СГРУППИРОВАТЬ ПО
		|	РегМаршрутаОтгрузки.ОтгрузкаКлиенту
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// 3. Выбираем данные из документов Отгрузка
		|ВЫБРАТЬ
		|	ОтгрузкаТЧ.Ссылка КАК Отгрузка,
		|	ОтгрузкаТЧ.Ссылка.Клиент КАК Клиент,
		|	СУММА(ОтгрузкаТЧ.Количество * ЕСТЬNULL(ОтгрузкаТЧ.Продукция.Вес, 0)) КАК Вес
		|ИЗ
		|	Документ.ОтгрузкаКлиенту.Продукция КАК ОтгрузкаТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_КлиентыМаршрутов КАК ВТ_Клиенты
		|		ПО ОтгрузкаТЧ.Ссылка.Клиент = ВТ_Клиенты.Клиент
		|ГДЕ
		|	ОтгрузкаТЧ.Ссылка.Проведен = ИСТИНА
		|	И НЕ ОтгрузкаТЧ.Ссылка В (ВЫБРАТЬ Т.Отгрузка ИЗ ВТ_ЗанятыеОтгрузки КАК Т)
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтгрузкаТЧ.Ссылка,
		|	ОтгрузкаТЧ.Ссылка.Клиент
		|
		|УПОРЯДОЧИТЬ ПО
		|	Клиент";
	
	Запрос.УстановитьПараметр("СписокМаршрутов", Объект.СписокМаршрутов.Выгрузить());
	Запрос.УстановитьПараметр("ТекущийДокумент", Объект.Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Объект.ОтгрузкиКлиентам.Добавить();
			НоваяСтрока.ОтгрузкаКлиенту = Выборка.Отгрузка;
			НоваяСтрока.Клиент = Выборка.Клиент;
			НоваяСтрока.Вес = Выборка.Вес;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры